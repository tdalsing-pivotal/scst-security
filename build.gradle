plugins {
    id "java"
    id "io.spring.dependency-management" version "1.1.3"
}

repositories {
    mavenCentral()
}

subprojects {
    apply plugin: "java"
    apply plugin: "io.spring.dependency-management"

    group = "com.vmware"
    version = "0.0.1-SNAPSHOT"

    java {
        sourceCompatibility = 17
        targetCompatibility = 17
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:3.1.4"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:2022.0.4"
            mavenBom "org.springframework.security:spring-security-bom:6.1.4"
        }
        dependencies {
            dependency "org.apache.commons:commons-lang3:3.13.0"
        }
    }

    dependencies {
        compileOnly "org.projectlombok:lombok:1.18.30"
        annotationProcessor "org.projectlombok:lombok:1.18.30"

        testCompileOnly "org.projectlombok:lombok:1.18.30"
        testAnnotationProcessor "org.projectlombok:lombok:1.18.30"

        testImplementation 'org.junit.jupiter:junit-jupiter-api'
    }

    test {
        useJUnitPlatform {
            includeTags 'unit'
            excludeTags 'integration'
        }
    }

    task integrationTest(type: Test) {
        useJUnitPlatform {
            includeTags 'integration'
            excludeTags 'unit'
        }
    }
}
