plugins {
    id "java"
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
}

repositories {
    mavenCentral()
}

subprojects {
    apply plugin: "java"
    apply plugin: "io.spring.dependency-management"

    group = "com.vmware"
    version = "0.0.1-SNAPSHOT"

    sourceCompatibility = 11
    targetCompatibility = 11

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    repositories {
        mavenCentral()
    }
    
    dependencyManagement {
        dependencies {
            dependency "org.springframework.boot:spring-boot-starter-web:2.2.6.RELEASE"
            dependency "org.springframework.cloud:spring-cloud-stream-binder-kafka:3.0.3.RELEASE"
            dependency "org.springframework.boot:spring-boot-starter-actuator:2.2.6.RELEASE"
            dependency 'org.springframework.boot:spring-boot-starter-security:2.2.6.RELEASE'
            dependency 'org.springframework.security:spring-security-oauth2-jose:5.3.1.RELEASE'
            dependency 'org.springframework.security:spring-security-oauth2-resource-server:5.3.1.RELEASE'
            dependency 'org.springframework.security:spring-security-oauth2-client:5.3.1.RELEASE'

            dependency "org.springframework.boot:spring-boot-starter-test:2.2.6.RELEASE"

            dependency "org.apache.commons:commons-lang3:3.10"
            dependency "ch.qos.logback:logback-classic:1.2.3"
            dependency "io.micrometer:micrometer-core:1.4.0"
            dependency "io.micrometer:micrometer-registry-influx:1.4.0"
        }
    }

    dependencies {
        compileOnly "org.projectlombok:lombok:1.18.12"
        annotationProcessor "org.projectlombok:lombok:1.18.12"

        testCompileOnly "org.projectlombok:lombok:1.18.12"
        testAnnotationProcessor "org.projectlombok:lombok:1.18.12"

        testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.0"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.6.0"

        testImplementation "org.mockito:mockito-core:3.3.3"
        testImplementation "org.assertj:assertj-core:3.15.0"
    }

    test {
        useJUnitPlatform {
            includeTags 'unit'
            excludeTags 'integration'
        }
    }

    task integrationTest(type: Test) {
        useJUnitPlatform {
            includeTags 'integration'
            excludeTags 'unit'
        }
    }
}
